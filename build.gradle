apply plugin: 'axelor-app'

// find all modules
def modules = new File('modules').listFiles().findAll { dir ->
	new File(dir, 'build.gradle').exists() && !dir.name.endsWith('-tests')
}

application {

	name "axelor-demo"
	title "Axelor :: Demo Application"
	
	version "4.0.0"

	// ADK version required
	adkVersion "~4.0.0"

	// depends on all modules
	modules.each { dir ->
		module "modules:$dir.name"
	}
}

tomcat {
	httpPort = project.properties.get("http.port", 8080) as Integer
	stopPort = 9451
}

def isProduction = {
	if (project.properties.get("application.mode", null) == "prod") return true;
	File configFile = rootProject.file("src/main/resources/application.properties");
	String mode = "dev"
	if (configFile.isFile()) {
		configFile.withReader { reader ->
			Properties config = new Properties()
			config.load(reader)
			mode = config.getProperty("application.mode");
		}
	}
	return mode == "prod"
}

tasks.build.dependsOn "grunt"
tasks.grunt.onlyIf isProduction
tasks.npm.onlyIf isProduction

// Link core projects, easy to develop along with core
ext.linkCoreInEclipse = true
